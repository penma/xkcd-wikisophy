#!/usr/bin/env perl

use strict;
use warnings;

use LWP::UserAgent;
use Text::Balanced qw(extract_bracketed);

my $pagename = $ARGV[0];

my $ua = LWP::UserAgent->new;
$ua->timeout(10);
$ua->env_proxy;

while (lc($pagename) ne "philosophy") {
	print "-> $pagename\n";

    my $response = $ua->get("https://secure.wikimedia.org/wikipedia/en/wiki/$pagename?action=raw");

    if (! $response->is_success)
    {
        die $response->status_line;
    }

    my $c = $response->decoded_content;

	wikiparse: while (length($c)) {
		if (defined($ARGV[1]) and $ARGV[1] eq "debug") {
			print $c;
			print "*" x 80 . "\n\n";
		}

		$c =~ s/<ref>.*?<\/ref>//gs;
		$c =~ s/'''?[^\n]'''?//s;

		if ($c =~ /^[^{(]+\[\[/s) {
			$c =~ s/^[^\[]+//s;
			($pagename, $c) = extract_bracketed($c);

			$pagename =~ s/^\[\[//;
			$pagename =~ s/\]\]$//;
			$pagename =~ s/\|.*//;

			if ($pagename =~ /:/) {
				next wikiparse;
			}

			last wikiparse;
		} else {
			$c =~ s/^[^{(]+//s;
			(undef, $c) = extract_bracketed($c);
		}
	} 
}

print "-> $pagename\n";
print "QED\n";
