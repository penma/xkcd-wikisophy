#!/usr/bin/env perl

use strict;
use warnings;

use Getopt::Long;
use LWP::UserAgent;
use Text::Balanced qw(extract_bracketed);
use URI;

my ($is_debug, $cache_file);
my $target = "philosophy";

GetOptions(
	"debug" => \$is_debug,
	"cache=s" => \$cache_file,
	"target=s" => \$target,
);

my $ua = LWP::UserAgent->new;
$ua->timeout(10);
$ua->env_proxy;

my %cached;

sub parse_page {
	my ($c) = @_;
	my $page_name;

	wikiparse: while (length($c)) {
		if ($is_debug) {
			print $c;
			print "*" x 80 . "\n\n";
		}

		$c =~ s/<ref>.*?<\/ref>//gs;
		$c =~ s/'''?[^\n]'''?//s;

		if ($c =~ /^[^{(]+\[\[/s) {
			$c =~ s/^[^\[]+//s;
			($page_name, $c) = extract_bracketed($c);

			$page_name =~ s/^\[\[//;
			$page_name =~ s/\]\]$//;
			$page_name =~ s/\|.*//;
			$page_name =~ s/#.*$//;

			if ($page_name =~ /:/) {
				next wikiparse;
			}

			last wikiparse;
		} else {
			$c =~ s/^[^{(]+//s;
			(undef, $c) = extract_bracketed($c);
		}
	}

	return $page_name;
}

sub process_page
{
	my ($pagename) = @_;

	my %encountered;
	my $num_encountered = 0;

	while (lc($pagename) ne $target && !$encountered{lc($pagename)}) {
		$encountered{lc($pagename)} = $num_encountered++;

		if ($cached{lc($pagename)}) {
			print "-* $pagename\n";
			$pagename = $cached{lc($pagename)};
			next;
		}

		print "-> $pagename\n";

		my $uri = URI->new("https://secure.wikimedia.org/wikipedia/en/w/index.php");
		$uri->query_form(action => "raw", title => $pagename);
		my $response = $ua->get($uri->as_string);

		if (! $response->is_success) {
			die $response->status_line;
		}

		my $new_pagename = parse_page($response->decoded_content);

		$cached{lc($pagename)} = $new_pagename;
		$pagename = $new_pagename;
	}

	print "-> $pagename\n";

	if ($encountered{lc($pagename)}) {
		print "Other infinite loop!!\n";
	} else {
		print "QED\n";
	}
}

if ($cache_file and open my $cache, '<', $cache_file) {
	while (<$cache>) {
		chomp;
		if (m/^([^#]+)#(.*)$/) {
			$cached{$1} = $2;
		}
	}
}

for (@ARGV) {
	process_page($_);
}

if ($cache_file) {
	open my $cache, '>', $cache_file or die "Unable to open $cache_file: $!";

	for (keys %cached) {
		print $cache "$_#$cached{$_}\n";
	}
}
