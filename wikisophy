#!/usr/bin/env perl

use strict;
use warnings;

use Getopt::Long;
use LWP::UserAgent;
use Text::Balanced qw(extract_bracketed);

my $is_debug;
my $target = "philosophy";

GetOptions(
	"debug" => \$is_debug,
	"target=s" => \$target,
);

my $pagename = shift(@ARGV);

my $ua = LWP::UserAgent->new;
$ua->timeout(10);
$ua->env_proxy;

my $num_encountered = 0;
my %encountered;

sub was_encountered {
	return $encountered{lc($pagename)};
}

while (lc($pagename) ne $target && (!was_encountered())) {
	$encountered{lc($pagename)} = $num_encountered++;

	print "-> $pagename\n";

	my $response = $ua->get("https://secure.wikimedia.org/wikipedia/en/wiki/$pagename?action=raw");

	if (! $response->is_success) {
		die $response->status_line;
	}

	my $c = $response->decoded_content;

	wikiparse: while (length($c)) {
		if ($is_debug) {
			print $c;
			print "*" x 80 . "\n\n";
		}

		$c =~ s/<ref>.*?<\/ref>//gs;
		$c =~ s/'''?[^\n]'''?//s;

		if ($c =~ /^[^{(]+\[\[/s) {
			$c =~ s/^[^\[]+//s;
			($pagename, $c) = extract_bracketed($c);

			$pagename =~ s/^\[\[//;
			$pagename =~ s/\]\]$//;
			$pagename =~ s/\|.*//;
			$pagename =~ s/#.*$//;

			if ($pagename =~ /:/) {
				next wikiparse;
			}

			last wikiparse;
		} else {
			$c =~ s/^[^{(]+//s;
			(undef, $c) = extract_bracketed($c);
		}
	} 
}

print "-> $pagename\n";

if (was_encountered()) {
	print "Other infinite loop!!\n";
}
else {
	print "QED\n";
}
